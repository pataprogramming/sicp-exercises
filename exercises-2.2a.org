#+TITLE: SICP Exercise Solutions for Section 2.2.1 and 2.2.2
#+AUTHOR: Paul L. Snyder
#+EMAIL: paul@pataprogramming.com
#+TODO: TODO(t) WRITEUP(w) || (d)

#+OPTIONS: num:0

#+LaTeX_HEADER: \usepackage{minted}
#+LaTeX_HEADER: \usepackage{color}
#+LaTeX_HEADER: \usepackage{xcolor}
#+LateX_HEADER: \usemintedstyle{friendly}
#+LaTeX_HEADER: \newminted{clojure}{fontsize=\large}
#+LaTeX_HEADER: \newminted{java}{fontsize=\large}
#+LaTeX_HEADER: \newminted{common-lisp}{fontsize=\large}
#+LaTeX_HEADER: \newminted{scheme}{fontsize=\large}

#+name: setup-minted
#+begin_src emacs-lisp :exports none :results silent
  (setq org-latex-listings 'minted)
  (add-to-list 'org-latex-packages-alist '("" "minted"))
  (setq org-latex-custom-lang-environments
        '(
         (emacs-lisp "common-lispcode")
         (scheme "schemecode")
         (lisp "common-lispcode")
         (java "javacode")
         (clojure "clojurecode")
          ))
  (setq org-latex-minted-options
        '(("frame" "lines")
          ("fontsize" "\\normalsize")
          ("linenos" "")))
  (setq org-latex-pdf-process
        '("pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
          "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
          "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"))
#+end_src

#+BEGIN_LaTeX
\newcommand{\red}[1]{{\color{red}#1}}
\newcommand{\orange}[1]{{\color{orange}#1}}
\newcommand{\purple}[1]{{\color{purple}#1}}
\definecolor{darkgreen}{HTML}{006B3C}
\newcommand{\green}[1]{{\color{darkgreen}#1}}
\newcommand{\blue}[1]{{\color{blue}#1}}
\definecolor{indigo}{HTML}{4B0082}
\newcommand{\indigo}[1]{{\color{indigo}#1}}
\newcommand{\java}{\red{Java}}
\newcommand{\lisp}{\red{Lisp}}
\newcommand{\clojure}{\blue{Clojure}}
#+END_LaTeX

#+BEGIN_SRC scheme :session 2-2b :results silent
  (require (planet neil/sicp:1:17))
#+END_SRC

#+BEGIN_SRC scheme :eval never :tangle 2-2b.rkt
  #lang planet neil/sicp
#+END_SRC

* 2.2.1 Representing Sequences
** Exercise 2.17: Defining =last-pair=
*** Problem
     Define a procedure =last-pair= that returns the
     list that contains only the last element of a given (nonempty)
     list:

#+BEGIN_EXAMPLE
          (last-pair (list 23 72 149 34))
          (34)
#+END_EXAMPLE

*** Solution

This problem is straightforward: just keep recursing on the =cdr= of
the current cell, but always peek ahead to see if it's empty; if it
is, then the last cons cell has been found.

It's is worth noting that this is different than the approach taken in
many situations, where the terminal case checks if the recursive
function has been called with =nil= (or another non-pair atom).  As a
consequence, =last-pair= never needs to check if the current cell is a
pair: as long as it is properly called with an initial list, it will
never recurse with =lst= as a non-pair parameter.

#+BEGIN_SRC scheme :session 2-2 :results output
  (define (last-pair lst)
    (if (null? (cdr lst))
        lst
        (last-pair (cdr lst))))

  (print (last-pair (list 23 72 149 34)))
#+END_SRC

#+RESULTS:
: '(34)

** Exercise 2.18: A =reverse= procedure
*** Problem
     Define a procedure =reverse= that takes a list as
     argument and returns a list of the same elements in reverse order:

#+BEGIN_EXAMPLE
          (reverse (list 1 4 9 16 25))
          (25 16 9 4 1)
#+END_EXAMPLE

*** Solution

This procedure takes advantage of a property of Scheme's
cons-cell-based lists: appending to the head of such a list is an
$O(1)$ operation.

In a na√Øve recursive implementation, the reverse procedure would
require $O(n)$ space as well as $O(n)$ time: a stack frame for every
call until the end of the list is reached, until the end is reached
and the final result can be assembled.

The shape of processes generated by procedures was, of course, a major
topic in Chapter 1. Here is an implementation of =reverse= that builds
a recursive iterative process, requiring $O(n)$ time but only $O(1)$
space.

#+BEGIN_SRC scheme :session 2-2 :results value
  (define (reverse lst)
    (define (iter lst ret)
      (if (null? lst)
          ret
          (iter (cdr lst) (cons (car lst) ret))))
    (iter lst null))

   (reverse (list 1 4 9 16 25))
#+END_SRC

#+RESULTS:
| 25 | 16 | 9 | 4 | 1 |

** TODO Exercise 2.19: Change-counting revisited
*** Problem
     Consider the change-counting program of section
     *Note 1-2-2::.  It would be nice to be able to easily change the
     currency used by the program, so that we could compute the number
     of ways to change a British pound, for example.  As the program is
     written, the knowledge of the currency is distributed partly into
     the procedure `first-denomination' and partly into the procedure
     `count-change' (which knows that there are five kinds of U.S.
     coins).  It would be nicer to be able to supply a list of coins to
     be used for making change.

     We want to rewrite the procedure `cc' so that its second argument
     is a list of the values of the coins to use rather than an integer
     specifying which coins to use.  We could then have lists that
     defined each kind of currency:

#+BEGIN_EXAMPLE
          (define us-coins (list 50 25 10 5 1))

          (define uk-coins (list 100 50 20 10 5 2 1 0.5))
#+END_EXAMPLE

     We could then call `cc' as follows:

#+BEGIN_EXAMPLE
          (cc 100 us-coins)
          292
#+END_EXAMPLE

     To do this will require changing the program `cc' somewhat.  It
     will still have the same form, but it will access its second
     argument differently, as follows:

          (define (cc amount coin-values)
            (cond ((= amount 0) 1)
                  ((or (< amount 0) (no-more? coin-values)) 0)
                  (else
                   (+ (cc amount
                          (except-first-denomination coin-values))
                      (cc (- amount
                             (first-denomination coin-values))
                          coin-values)))))

     Define the procedures `first-denomination',
     `except-first-denomination', and `no-more?' in terms of primitive
     operations on list structures.  Does the order of the list
     `coin-values' affect the answer produced by `cc'?  Why or why not?

*** Solution
** Exercise 2.20:
*** Problem
     The procedures =+=, =*=, and =list= take
     arbitrary numbers of arguments. One way to define such procedures
     is to use =define= with notation "dotted-tail notation".  In a
     procedure definition, a parameter list that has a dot before the
     last parameter name indicates that, when the procedure is called,
     the initial parameters (if any) will have as values the initial
     arguments, as usual, but the final parameter's value will be a "list"
     of any remaining arguments.  For instance, given the definition

#+BEGIN_EXAMPLE
          (define (f x y . z) <BODY>)
#+END_EXAMPLE

     the procedure =f= can be called with two or more arguments.  If we
     evaluate

#+BEGIN_EXAMPLE
          (f 1 2 3 4 5 6)
#+END_EXAMPLE

     then in the body of =f=, =x= will be 1, =y= will be 2, and =z=
     will be the list =(3 4 5 6)=.  Given the definition

#+BEGIN_EXAMPLE
          (define (g . w) <BODY>)
#+END_EXAMPLE

     the procedure =g= can be called with zero or more arguments.  If we
     evaluate

#+BEGIN_EXAMPLE
          (g 1 2 3 4 5 6)
#+END_EXAMPLE

     then in the body of =g=, =w= will be the list =(1 2 3 4 5 6)=.(4)

     Use this notation to write a procedure =same-parity= that takes
     one or more integers and returns a list of all the arguments that
     have the same even-odd parity as the first argument.  For example,

#+BEGIN_EXAMPLE
          (same-parity 1 2 3 4 5 6 7)
          (1 3 5 7)

          (same-parity 2 3 4 5 6 7)
          (2 4 6)
#+END_EXAMPLE

*** Solution

As in the previous exercise, this solution uses a helper function to
achieve an iterative style. It alse defines a second helper, =match=,
that closes over the first value in the list supplied to
=same-parity=, and can then be used to compare the parity of any later
element in the list. This makes the condition of the =cond= in =iter=
that relies on it more succinct, as well as making the intention more
explicit.

#+BEGIN_SRC scheme :session 2-2 :results silent
    (define (same-parity key . vals)
      (define (matches? val) (= (remainder key 2) (remainder val 2)))
      (define (iter ret vals)
        (cond ((null? vals)          (reverse ret))
              ((matches? (car vals)) (iter (cons (car vals) ret)
                                           (cdr vals)))
              (true                  (iter ret (cdr vals)))))
      (iter (list key) vals))
#+END_SRC

This produces the desired behavior, but note the use of the previously
define =reverse= procedure in the =null?= condition clause.  The
property that made $cons$ convenient for reversing a list makes it
similarly inconvenient to build a list from start to end. =reverse=
puts the list into the correct order, but does take a cost in
time. Fortunately, both =reverse= and =same-parity= are $O(n)$, so the
computational complexity is not increased, it is scaled by a
constant factor.

The procedure's results are those prescribed by the exercise:

#+BEGIN_SRC scheme :session 2-2 :results output
  (print (same-parity 1 2 3 4 5 6 7))
  (newline)
  (print (same-parity 2 3 4 5 6 7))
#+END_SRC

#+RESULTS:
: '(1 3 5 7)
: '(2 4 6)

** WRITEUP Exercise 2.21: Squaring a list
*** Problem
     The procedure =square-list= takes a list of
     numbers as argument and returns a list of the squares of those
     numbers.

#+BEGIN_EXAMPLE
          (square-list (list 1 2 3 4))
          (1 4 9 16)
#+END_EXAMPLE

     Here are two different definitions of =square-list=.  Complete
     both of them by filling in the missing expressions:

#+BEGIN_EXAMPLE
          (define (square-list items)
            (if (null? items)
                nil
                (cons <??> <??>)))

          (define (square-list items)
            (map <??> <??>))
#+END_EXAMPLE

*** Solution

#+BEGIN_SRC scheme :session 2-2
  (define (square-list-1 items)
    (if (null? items)
        null
        (cons (* (car items) (car items)) (square-list-1 (cdr items)))))

  (define (square-list-2 items)
    (map (lambda (x) (* x x)) items))
#+END_SRC

#+RESULTS:

** WRITEUP Exercise 2.22: An unintended reversal
*** Problem
     Louis Reasoner tries to rewrite the first
     =square-list= procedure of *Note Exercise 2-21:: so that it
     evolves an iterative process:

#+BEGIN_EXAMPLE
          (define (square-list items)
            (define (iter things answer)
              (if (null? things)
                  answer
                  (iter (cdr things)
                        (cons (square (car things))
                              answer))))
            (iter items nil))
#+END_EXAMPLE

     Unfortunately, defining =square-list= this way produces the answer
     list in the reverse order of the one desired.  Why?

     Louis then tries to fix his bug by interchanging the arguments to
     =cons=:

#+BEGIN_EXAMPLE
          (define (square-list items)
            (define (iter things answer)
              (if (null? things)
                  answer
                  (iter (cdr things)
                        (cons answer
                              (square (car things))))))
            (iter items nil))
#+END_EXAMPLE

     This doesn't work either.  Explain.

*** Solution

For the first example, with each recursive call to =iter=, the =cons=
adds successive items to the left side of the list: when using =cons=,
an item added to a list is appended to the beginning (since the only
$O(1)$ access using the pointer to the cell at the start of the list.

For the second, the procedure builds an ill-formed data
structure. Scheme only treats the pattern of cons cells as a list if,
for each cons structure, the left cell holds a data item and the right
cell holds either a cons cell representing a properly structured list
or =nil=.

** WRITEUP Exercise 2.23: Implementing =for-each=
*** Problem
     The procedure =for-each= is similar to =map=.  It
     takes as arguments a procedure and a list of elements.  However,
     rather than forming a list of the results, =for-each= just applies
     the procedure to each of the elements in turn, from left to right.
     The values returned by applying the procedure to the elements are
     not used at all--=for-each= is used with procedures that perform
     an action, such as printing.  For example,

#+BEGIN_EXAMPLE
          (for-each (lambda (x) (newline) (display x))
                    (list 57 321 88))
          57
          321
          88
#+END_EXAMPLE

     The value returned by the call to =for-each= (not illustrated
     above) can be something arbitrary, such as true.  Give an
     implementation of =for-each=.

*** Solution

#+BEGIN_SRC scheme :session 2-2 :results silent
  (define (for-each proc items)
    (if (null? items)
        null
        (begin
          (proc (car items))
          (for-each proc (cdr items)))))
#+END_SRC

#+BEGIN_SRC scheme :session 2-2 :results output
(for-each (lambda (x) (print "woo:") (print x) (newline)) '(1 2 5 6 7))
#+END_SRC

#+RESULTS:
: "woo:"1
: "woo:"2
: "woo:"5
: "woo:"6
: "woo:"7


* 2.2.2 Hierarchical Structures
** TODO Exercise 2.24: Box-and-pointer representation
*** Problem
     Suppose we evaluate the expression =(list 1 (list
     2 (list 3 4)))=.  Give the result printed by the interpreter, the
     corresponding box-and-pointer structure, and the interpretation of
     this as a tree (as in *Note Figure 2-6::).

*** Solution

#+BEGIN_SRC scheme :session 2-2 :results output
(print (list 1 (list 2 (list 3 4))))
#+END_SRC

#+RESULTS:
: '(1 (2 (3 4)))

#+BEGIN_SRC dot :file boxes-2-2.png :export results
  digraph {

  n1 [label=1,shape=record];
  n2 [label=2,shape=record];
  n3 [label=3,shape=record];
  n4 [label=4,shape=record];
  nil1 [label=0,shape=record];
  nil2 [label=0,shape=record];
  nil3 [label=0,shape=record];
  c1-1 [level=1,shape=record,label="{<car>|<cdr>}"];
  c1-2 [level=1,shape=record,label="{<car>|<cdr>}"];
  c2-1 [level=2,shape=record,label="{<car>|<cdr>}"];
  c2-2 [level=2,shape=record,label="{<car>|<cdr>}"];
  c3-1 [level=3,shape=record,label="{<car>|<cdr>}"];
  c3-2 [level=3,shape=record,label="{<car>|<cdr>}"];

  c11:car -> n1;
  c11:cdr -> c12;
  c12:car -> c21;
  c12:cdr -> nil1;

  c21:car -> n2;
  c21:cdr -> c22;
  c22:car -> c31;
  c22:cdr -> nil2;

  c31:car -> n3;
  c31:cdr -> c32;
  c32:car -> n4;
  c32:cdr -> nil3



  }
#+END_SRC

#+RESULTS:
[[file:boxes-2-2.png]]

** WRITEUP Exercise 2.25: Selecting with =car= and =cdr=
*** Problem
     Give combinations of =car=s and =cdr=s that will
     pick 7 from each of the following lists:

#+BEGIN_EXAMPLE
          (1 3 (5 7) 9)

          ((7))

          (1 (2 (3 (4 (5 (6 7))))))
#+END_EXAMPLE

*** Solution

#+BEGIN_SRC scheme :session 2-2 :results output
  (define l1 (list 1 3 (list 5 7) 9))
  (define l2 (list (list 7)))
  (define l3 (list 1 (list 2 (list 3 (list 4 (list 5 (list 6 7)))))))

  (print (car (cdr (car (cdr (cdr l1))))))
  (newline)
  (print (car (car l2)))
  (newline)
  (print
   (car (cdr (car (cdr (car (cdr (car (cdr (car (cdr (car (cdr l3)))))))))))))
  (newline)

  (define (iterate f n x)
    (if (<= n 0)
        x
        (iterate f (- n 1) (f x))))

  (println (iterate (compose car cdr) 6 l3))
#+END_SRC

#+RESULTS:
: An error occurred.

** WRITEUP Exercise 2.26: Basic list operations
*** Problem
     Suppose we define =x= and =y= to be two lists:

#+BEGIN_EXAMPLE
          (define x (list 1 2))
          (define y (list 4 5 6))
#+END_EXAMPLE

     What result is printed by the interpreter in response to
     evaluating each of the following expressions:

#+BEGIN_EXAMPLE
          (append x y)
          (cons x y)
          (list x y)
#+END_EXAMPLE

*** Solution
#+BEGIN_SRC scheme :session 2-2 :results output
  (define x (list 1 2))
  (define y (list 4 5 6))

  (print (append x y))
  (newline)
  (print (cons x y))
  (newline)
  (print (list x y))
#+END_SRC

#+RESULTS:
: '(1 2 4 5 6)
: '((1 2) 4 5 6)
: '((1 2) (4 5 6))

** WRITEUP Exercise 2.27: Sublist reversal with =deep-reverse=
*** Problem
     Modify your =reverse= procedure of *Note Exercise
     2-18:: to produce a =deep-reverse= procedure that takes a list as
     argument and returns as its value the list with its elements
     reversed and with all sublists deep-reversed as well.  For example,

#+BEGIN_EXAMPLE
          (define x (list (list 1 2) (list 3 4)))

          x
          ((1 2) (3 4))

          (reverse x)
          ((3 4) (1 2))

          (deep-reverse x)
          ((4 3) (2 1))
#+END_EXAMPLE

*** Solution

#+BEGIN_SRC scheme :session 2-2
  (define (deep-reverse lst)
    (define (recurse lst ret)
      (if (null? lst) ret
          (if (pair? lst)
              (recurse (cdr lst) (cons (deep-reverse (car lst)) ret))
              lst)))
    (recurse lst null))

  (define (deep-reverse-2 lst)
    (define (recurse lst ret)
      (if (null? lst) ret
          (if (pair? lst)
              (recurse (cdr lst) (cons (recurse (car lst) null) ret))
              lst)))
    (recurse lst null))

  (define z (list (list 1 2 3) (list 3 4)))

  (reverse z)

  (deep-reverse z)
#+END_SRC

#+RESULTS:
| 4 | 3 |   |
| 3 | 2 | 1 |

** WRITEUP Exercise 2.28: Plucking the leaves with =fringe=
*** Problem
     Write a procedure =fringe= that takes as argument
     a tree (represented as a list) and returns a list whose elements
     are all the leaves of the tree arranged in left-to-right order.
     For example,

#+BEGIN_EXAMPLE
          (define x (list (list 1 2) (list 3 4)))

          (fringe x)
          (1 2 3 4)

          (fringe (list x x))
          (1 2 3 4 1 2 3 4)
#+END_EXAMPLE

*** Solution

#+BEGIN_SRC scheme :session 2-2
  (define (fringe tr)
    (if (not (pair? tr))
        tr
        (let ((head (car tr))
              (tail (cdr tr)))
          (if (pair? head)
              (append (fringe head) (fringe tail))
              (cons head (fringe tail))))))

  (define w (list (list 1 2) (list 3 4)))

  (fringe w)
  (fringe (list w w))
  (fringe '(1 2))
#+END_SRC

#+RESULTS:
| 1 | 2 |

** TODO Exercise 2.29: Balancing a mobile
*** Problem
     A binary mobile consists of two branches, a left
     branch and a right branch.  Each branch is a rod of a certain
     length, from which hangs either a weight or another binary mobile.
     We can represent a binary mobile using compound data by
     constructing it from two branches (for example, using =list=):

#+BEGIN_EXAMPLE
          (define (make-mobile left right)
            (list left right))
#+END_EXAMPLE
     A branch is constructed from a =length= (which must be a number)
     together with a =structure=, which may be either a number
     (representing a simple weight) or another mobile:

#+BEGIN_EXAMPLE
          (define (make-branch length structure)
            (list length structure))
#+END_EXAMPLE

       a. Write the corresponding selectors =left-branch= and
          =right-branch=, which return the branches of a mobile, and
          =branch-length= and =branch-structure=, which return the
          components of a branch.

       b. Using your selectors, define a procedure =total-weight= that
          returns the total weight of a mobile.

       c. A mobile is said to be "balanced" if the torque applied by
          its top-left branch is equal to that applied by its top-right
          branch (that is, if the length of the left rod multiplied by
          the weight hanging from that rod is equal to the
          corresponding product for the right side) and if each of the
          submobiles hanging off its branches is balanced. Design a
          predicate that tests whether a binary mobile is balanced.

       d. Suppose we change the representation of mobiles so that the
          constructors are

#+BEGIN_EXAMPLE
               (define (make-mobile left right)
                 (cons left right))

               (define (make-branch length structure)
                 (cons length structure))
#+END_EXAMPLE

          How much do you need to change your programs to convert to
          the new representation?

*** Solution
** WRITEUP Exercise 2.30: Squaring a tree
*** Problem
     Define a procedure =square-tree= analogous to the
     =square-list= procedure of *Note Exercise 2-21::.  That is,
     =square-list= should behave as follows:

#+BEGIN_EXAMPLE
          (square-tree
           (list 1
                 (list 2 (list 3 4) 5)
                 (list 6 7)))
          (1 (4 (9 16) 25) (36 49))
#+END_EXAMPLE

     Define =square-tree= both directly (i.e., without using any
     higher-order procedures) and also by using =map= and recursion.

*** Solution

#+BEGIN_SRC scheme :session 2-2 :results output
  (define (square-tree-1 tr)
    (cond ((null? tr) null)
          ((not (pair? tr)) (* tr tr))
          (true (cons (square-tree-1 (car tr)) (square-tree-1 (cdr tr))))))

  (define (square-tree-2 tr)
    (define (square x) (* x x))
    (cond ((null? tr) null)
          ((not   (pair? tr)) (square tr))
          (true   (map square-tree-2 tr))))

  (define num-tree
    (list 1
          (list 2 (list 3 4) 5)
          (list 6 7)))

  (print (square-tree-1 num-tree))
  (newline)
  (print (square-tree-2 num-tree))
  (newline)
#+END_SRC

#+RESULTS:
: '(1 (4 (9 16) 25) (36 49))
: '(1 (4 (9 16) 25) (36 49))

** WRITEUP Exercise 2.31: Defining a =tree-map=
*** Problem
     Abstract your answer to *Note Exercise 2-30:: to
     produce a procedure =tree-map= with the property that
     =square-tree= could be defined as

#+BEGIN_EXAMPLE
          (define (square-tree tree) (tree-map square tree))
#+END_EXAMPLE

*** Solution

#+BEGIN_SRC scheme :session 2-2 :results output
  (define (square x) (* x x))

  (define (tree-map f tr)
    (define (tree-map-h t) (tree-map f t))
    (cond ((null? tr) null)
          ((not   (pair? tr)) (f tr))
          (true   (map tree-map-h tr))))

  (define (square-tree-3 tr)
    (tree-map square tr))

  (define num-tree
    (list 1
          (list 2 (list 3 4) 5)
          (list 6 7)))

  (print (square-tree-3 num-tree))
#+END_SRC

#+RESULTS:
: '(1 (4 (9 16) 25) (36 49))

** WRITEUP Exercise 2.32: Generating powersets
*** Problem
     We can represent a set as a list of distinct
     elements, and we can represent the set of all subsets of the set as
     a list of lists.  For example, if the set is =(1 2 3)=, then the
     set of all subsets is =(() (3) (2) (2 3) (1) (1 3) (1 2) (1 2
     3))=.  Complete the following definition of a procedure that
     generates the set of subsets of a set and give a clear explanation
     of why it works:

#+BEGIN_EXAMPLE
          (define (subsets s)
            (if (null? s)
                (list nil)
                (let ((rest (subsets (cdr s))))
                  (append rest (map <??> rest)))))
#+END_EXAMPLE

*** Solution

#+BEGIN_SRC scheme :session 2-2 :results output
  (define (subsets s)
    (if (null? s)
        (list null)
        (let ((rest (subsets (cdr s))))
          (append rest (map (lambda (t) (cons (car s) t)) rest)))))

  (print (subsets (list 1 2 3)))
#+END_SRC

#+RESULTS:
: '(() (3) (2) (2 3) (1) (1 3) (1 2) (1 2 3))

For a given set $S$, the set of all its subsets are frequently called
its /power set/.  A natural way to generate a set's power set is
recursively: pick an element $x$ of the set $S$; the power set is then
the set of all the subsets of $S$ that do not contain $e$, combined
with all subsets that do contain $e$.
